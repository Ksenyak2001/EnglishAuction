
FUNCTION_BLOCK count_rate_of_price_increase	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	excitement : REAL;
	fear_of_loss : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	rate_of_price_increase : REAL;
END_VAR

FUZZIFY excitement			
	TERM low := (0, 1) (2.5, 1) (5,0) ;
	TERM avg := trian 2.5 5 7.5;
	TERM high := (5, 0) (7.5, 1) (10, 1);
END_FUZZIFY

FUZZIFY fear_of_loss		
	TERM low := (0, 1) (4,0) ;
	TERM low_avg := trian 2 4 6;
	TERM high_avg := trian 4 6 8;
	TERM high := (6, 0) (10,1);	
END_FUZZIFY

DEFUZZIFY rate_of_price_increase		
	TERM zero := (0,1) (1,0);
	TERM one := trian 0 1 2;
	TERM two := trian 1 2 3;
	TERM three := trian 2 3 4;
	TERM four := trian 3 4 5;
	TERM five := trian 4 5 6;
	TERM six := trian 5 6 7;
	TERM seven := trian 6 7 8;
	TERM eight := trian 7 8 9;
	TERM nine := trian 8 9 10;
	TERM ten := (9,0) (10,1);
	
	METHOD : COG;		
	DEFAULT := -2;		
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF (excitement IS low AND fear_of_loss IS low) THEN rate_of_price_increase IS zero;
	RULE 2 : IF (excitement IS low AND fear_of_loss IS low_avg) THEN rate_of_price_increase IS one;
	RULE 3 : IF (excitement IS low AND fear_of_loss IS high_avg) THEN rate_of_price_increase IS two;
	RULE 4 : IF (excitement IS avg AND fear_of_loss IS low) THEN rate_of_price_increase IS three;
	RULE 5 : IF (excitement IS low AND fear_of_loss IS high) THEN rate_of_price_increase IS four;
	RULE 6 : IF (excitement IS avg AND fear_of_loss IS low_avg) THEN rate_of_price_increase IS five;
	RULE 7 : IF (excitement IS avg AND fear_of_loss IS high_avg) THEN rate_of_price_increase IS six;
	RULE 8 : IF (excitement IS high AND fear_of_loss IS low) THEN rate_of_price_increase IS seven;
	RULE 9 : IF (excitement IS avg AND fear_of_loss IS high) THEN rate_of_price_increase IS eight;
	RULE 10 : IF (excitement IS high AND fear_of_loss IS low_avg) THEN rate_of_price_increase IS eight;
	RULE 11 : IF (excitement IS high AND fear_of_loss IS high_avg) THEN rate_of_price_increase IS nine;
	RULE 12 : IF (excitement IS high AND fear_of_loss IS high) THEN rate_of_price_increase IS ten;
	
	
END_RULEBLOCK

END_FUNCTION_BLOCK
